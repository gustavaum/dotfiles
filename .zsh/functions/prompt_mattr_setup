# clint prompt theme

prompt_mattr_help () {
  cat <<'EOF'

  prompt mattr [<color1> [<color2> [<color3> [<color4> [<color5>]]]]]

  defaults are yellow, cyan, green, yellow, and white, respectively.

EOF
}

prompt_mattr_setup () {
  local -a pcc
  local -A pc
  local p_date p_tty p_plat p_userpwd p_apm p_shlvlhist p_rc p_end p_win

  autoload -Uz vcs_info

  pcc[1]=${1:-'yellow'}
  pcc[2]=${2:-'cyan'}
  pcc[3]=${3:-'green'}
  pcc[4]=${4:-'red'}
  pcc[5]=${5:-'white'}

  pc['\[']=" %F{$pcc[1]}:%f "
  pc['<']="%F{$pcc[1]}<"
  pc['>']="%F{$pcc[1]}>"
  pc['\(']="%F{$pcc[1]}("
  pc['\)']="%F{$pcc[1]})"

  p_filler="%B$pc['\[']%b"
  p_date="%F{$pcc[2]}%D{%a %F %r}%f"
  p_tty="%l"

  [[ -n "$WINDOW" ]] && p_win="$pc['\(']%B%F{$pcc[4]}$WINDOW%b%f$pc['\)']"

  p_pwd="%F{$pcc[2]}%~%f"
  p_user="%m$p_win"
  p_vcs="${vcs_info_msg_0_}"

  p_shlvlhist="%fzsh%(2L./$SHLVL.)"
  p_rc="%(?..[%?%1v])"
  p_end="%f%B%#%b "

  zle_highlight[(r)default:*]=default:$pcc[2]
  
  prompt="$p_user$p_filler$p_tty$p_filler$p_vcs
$p_pwd$p_rc $p_end"
  PS2='%(4_.\.)%3_> %E'

  add-zsh-hook precmd prompt_mattr_precmd
}

prompt_mattr_precmd () {
  setopt noxtrace noksharrays localoptions
  local exitstatus=$?

  psvar=()
  [[ $exitstatus -ge 128 ]] && psvar[1]=" $signals[$exitstatus-127]" ||
	psvar[1]=""

  vcs_info
}

prompt_mattr_setup "$@"
